# Лабораторная работа №6 `98252`
Выполнил: **Дорохин Сергей Константинович `группа: P3111`** \
Преподаватель: **Саржвеский Иван Анатольевич**

------------
### Условие
По выданному преподавателем варианту разработать и исследовать работу комплекса программ обмена данными в режиме
прерывания программы. Основная программа должна изменять содержимое заданной ячейки памяти (Х), которое должно быть
представлено как знаковое число. Область допустимых значений изменения Х должна быть ограничена заданной функцией F(X) и
конструктивными особенностями регистра данных ВУ (8-ми битное знаковое представление). Программа обработки прерывания
должна выводить на ВУ модифицированное значение Х в соответствии с вариантом задания, а также игнорировать все
необрабатываемые прерывания.

------------
### Задания варианта
* Основная программа должна увеличивать на 2 содержимое X (ячейки памяти с адресом $011_{16}$) в цикле.
*  Обработчик прерывания должен по нажатию кнопки готовности ВУ-1 осуществлять вывод результата вычисления функции $F(X)=2X+1$ на данное ВУ, a по нажатию кнопки готовности ВУ-3 вычесть утроенное содержимое РД данного ВУ из Х, результат записать в X
* Если Х оказывается вне ОДЗ при выполнении любой операции по его изменению, то необходимо в Х записать минимальное по ОДЗ число.

------------
### ОДЗ

$-128 \leq 2x+1 \leq 127 \Rightarrow -127 \leq 2x \leq 128 \Rightarrow -63.5 \leq x \leq 64 \Rightarrow -63 \leq x \leq 64$

$63 = \textbf{0x003F}$
&
$64 = \textbf{0x0040}$
&
$-63 = 1111.1111.1100.0001 = \textbf{0xFFC1} $

Таким образом, число X лежит в диапазоне **[FFC1; 0040]**

------------

### Расположение данных в памяти

* 0x000-0x00F – вектора прерываний
* 0x011: X & 0x012: MAX & 0x013: MIN
* 0x069: INT3 & 0x079: INT1
* 0x060: MAIN - основная программа

------------

### Область представления данных

* X, MIN, MAX - знаковые 16-ричные числа
* DR КВУ - знаковое 8-разрядное число

------------

### Текст программы

```asm
ORG 0x000
; использование векторов 1,3
V0:	WORD $DEFAULT, 0x180
V1:	WORD $INT1, 0x180
V2:	WORD $DEFAULT, 0x180
V3:	WORD $INT3, 0x180
V4:	WORD $DEFAULT, 0x180
V5:	WORD $DEFAULT, 0x180
V6:	WORD $DEFAULT, 0x180
V7:	WORD $DEFAULT, 0x180
DEFAULT:	IRET

ORG 0x011
X: WORD 0x6      ; входное число
MAX: WORD 0x0040 ; максимальное число - 64
MIN: WORD 0xFFC1 ; минимальное число - -63

ORG 0x050
; входная точка
START:
    DI      ; запрет прерывания
    ; подготовка ВУ1 и ВУ3
    CLA
    OUT 0x1
    OUT 0x5
    OUT 0xB
    OUT 0xD
    OUT 0x11
    OUT 0x15
    OUT 0x19
    OUT 0x1D
    LD #0x9
    OUT 3
    LD #0xB
    OUT 7
    ; конец подготовки
    EI      ; разрешение прерывания
    CLA

MAIN:
    DI          ; запрет прерывания
    LD X        ; загрузка X в AC
    ; Основная программа должна увеличивать на 2 содержимое X 
    INC         ; +1
    INC         ; +1
    CALL $CHECK ; проверка ОДЗ
    ST X        ; сохранение числа
    NOP         ; регистр PS сохранен
    EI          ; разрешение на прерывание
    JUMP MAIN   ; цикл

INT3:
    DI          ; запрет прерывания
    CLA         ; очистка аккумулятора
    IN 6        ; загрузка из DR (ВУ-3)
    ; вычесть утроенное содержимое РД данного ВУ из Х, результат записать в X
    PUSH
    CLA
    ADD &0
    ADD &0
    ADD &0
    PUSH        ; на &0 лежит утроенное содержимое РД данного ВУ
    LD X
    SUB &0      ; выитаем &0 из X
    ST X        ; сохраняем новое значение в X
    ; конец операции
    NOP         ; регистр PS сохранен
    POP
    POP
    IRET        ; возврат из прерывания
    
INT1:
    DI          ; запрет на прерывание
    LD X        ; загрузка X
    NOP         ; регистр PS сохранен
    ; вычисление F(x) = 2X + 1
    ASL
    ADD #1
    ; конец вычислений
    OUT 2       ; вывод числа в ВУ-1
    NOP         ; регистр PS сохранен
    EI          ; разрешение на прерывания
    IRET        ; возврат из прерывания

CHECK:
check_min:
	NOP
	CMP MIN
    	BPL check_max
   	JUMP ld_min
check_max:
	CMP MAX
	BEQ return
    	BMI return
ld_min:
	LD MIN
return:
	RET

```

------------

### Методика проверки

1. Загрузить текст программы в БЭВМ 
2. Заменить `NOP` по нужному адресу на `HLT` 
3. Запустить программу в режиме `РАБОТА`
4. Установить `Готовность ВУ-1`
5. Дождаться остановки 
6. Записать текущее значение `X` из памяти БЭВМ:
> 1. Запомнить текущее состояние счетчика команд
> 2. Ввести в клавишный регистр значение `0x011`
> 3. Нажать `«Ввод адреса»`.
> 4. Нажать `«Чтение»`.
> 5. Записать значение регистра данных
> 6. Вернуть счетчик команд в исходное состояние
7. Записать результат обработки прерывания – полученное значение F(X) = 2X + 1 из DR контроллера ВУ-1.
8. Рассчитать ожидаемое значение обработки прерывания ($F(x)$)
9. Нажать `«Продолжение»`.
10. Ввести в `ВУ-3` произвольное число, записать его.
11. Установить `«Готовность ВУ-3»`.
12. Дождаться остановки
13. Записать текущее значение `X` из памяти БЭВМ, также, как и в пункте 6.
14. Нажать `«Продолжение»`.
15. Записать результат обработки прерывания, как в пункте 6.
16. Рассчитать ожидаемое значение переменной `X` после обработки прерывания

------------

### Проверка основной программы

1. Загрузить текст программы в БЭВМ.
2. Записать в переменную `X` минимальное по ОДЗ значение `-63`
3. Запустить программу в режиме останова.
4. Пройти нужное количество шагов программы, убедиться, что при увеличении `X` на `2`, до после момента, когда он равен `64`, происходит сброс значения в минимальное по ОДЗ

------------

### Заключение

В ходе выполнения лабораторной работы я изучил обмен данными с ВУ-1 и ВУ-3 в режиме прерываний, закрепил знания в написании программ на ассемблере БЭВМ.

